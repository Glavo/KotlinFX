buildscript {
    ext.kotlinVersion = "1.4.31"
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.openjfx:javafx-plugin:0.0.9"
    }
}

allprojects {
    group 'org.glavo'
    version '0.3.0-SHAPSHOT'

    apply plugin: 'java-library'

    java {
        modularity.inferModulePath.set(true)
    }

    compileJava {
        sourceCompatibility = 9
        targetCompatibility = 9
    }

    compileJava.doLast {
        FileTree tree = fileTree('build/classes/java/main')
        tree.include '**/*.class'
        tree.exclude 'module-info.class'
        tree.each {
            RandomAccessFile rf = new RandomAccessFile(it, "rw")
            rf.seek(7)   // major version
            rf.write(52) // java 8
            rf.close()
        }
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = '11.0.2'
        modules = ['javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.media', 'javafx.swing', 'javafx.web']
        configuration = "compileOnly"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    }

    rootProject.dependencies {
        api project
    }
}


allprojects {
    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    publishing {
        publications {
            Kala(MavenPublication) {
                from components.java

                artifact sourcesJar

                pom {
                    name = project.name
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'glavo'
                            name = 'Glavo'
                            email = 'zjx001202@gmail.com'
                        }
                    }
                }
            }
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}