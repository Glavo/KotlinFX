buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

allprojects {
    group 'asia.kala'
    version '0.2.0-SNAPSHOT'

    apply plugin: 'java-library'
}

subprojects {
    apply plugin: 'kotlin'

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    }

    rootProject.dependencies {
        api project
    }
}


allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    publishing {
        publications {
            Kala(MavenPublication) {
                from components.java

                artifact sourcesJar

                pom {
                    name = project.name
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'glavo'
                            name = 'Glavo'
                            email = 'zjx001202@gmail.com'
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['Kala']
        pkg {
            repo = 'maven'
            name = project.name
            licenses = ['Apache-2.0']
            publicDownloadNumbers = true
            vcsUrl = 'https://github.com/kala-projects/kotlinfx.git'
            version {
                name = project.version
                desc = "${project.name} ${project.version}"
                vcsTag = project.version
            }
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    task travisPublish() {}

    if (!project.version.toString().endsWith("SHAPSHOT")) {
        travisPublish.dependsOn(bintrayUpload)
    }
}